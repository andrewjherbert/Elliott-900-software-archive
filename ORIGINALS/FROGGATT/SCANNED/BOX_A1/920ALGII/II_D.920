
( INPUT OUTPUT )

LDZERO   +0

IDEVTB   +2048 ( READER 1 )
         +2048 ( READER 2 NOT USED )
         +2052 ( READER 3 : TELEPRINTER )


ODEVTB   +6144 ( PUNCH 1 )
         +6144 ( PUNCH 2 )
         +6148 ( PUNCH 3 : TELEPRINTER )
         +4352 ( PUNCH 4 : LINE PRINTER )


INOEX    0  IOLNK
         /8  1


RNDTBL
8  0
+26214
+10485
+4194
+1677
+671
+268
+107
+43
+17
+7
+3
+1
+0
+0
+0



INOUT
         2  +15
         9  INOUT1
         4  +0
INOUT2   5 W10
         4  +3
         2  SP
         5  W
         0  ADPART
         /0 INOUT3
         11 IOLNK
         /8 0
         8  NXPORD

INOUT1   4  +7
         8  INOUT2

INOUT4   4  -7
         5  WS
INOUT5   0  WS
         /4 GLOPMD+7
         /5 GLOPMD+14
         10 WS
         4  WS
         9  INOUT5
         8  NXPORD

SMLN     4  NULSTR+2
         8  PRFX1

PRFX     5  SP
         0  W
         /4  0
PRFX1    0  W10
         /5 GLPRFA
INOUT3   8  NXPORD
         0  RDNM
         0  RDNM
         0  OUTI
         0  OUTR
         0  ALGN
         0  DEST
         0  DIGIT
         0  FRPNT
         0  SPARE
         0  SPARE ( 10 )

         0  PRFX
         0  SMLN
         0  SCLD
         0  SRCE
         0  OSTR1
         0  ALGN
         0  DEST
         0  DIGIT
         0  FRPNT
         0  INOUT4 (20)

         0  SPARE
         0  PRFX
         0  SMLN
         0  SCLD
         0  SRCE


FRPNT    5  SP
         0  W
         /4  0
FRPNT1   0  W10
         /5 GLDIGN
         5  W1
         4  +0
         /5 GLOPMD
INOUT9   4  W1
         1  -1
         9  FRPNT2
         1  -8
         9  NXPORD
FRPNT2   4  +7
         8  FRPNT1

ALGN     1  -3
         5  SP
         5  W
         0  W
         /4  3
         0  W10
         9  FRPNT2
         /5 GLDIGN
         4  -1
         /5 GLOPMD
         0  W
         /4 0
         0  W10
         9  FRPNT2
         /5 GLDIGM
         /1 GLDIGN
         1  -1
         9  FRPNT2
         2  +14
         9  FRPNT2
         8  NXPORD

SCLD     5  SP
         0  W
         /4  0
         0  W10
         /5 GLDIGN
         5  W1
         4  +1
         /5 GLOPMD
         8  INOUT9

DEST     5  SP
         0  W
         /4 0
DEST1    0  W10
         /5 GLODEV
         9  DEST2
         7  DEST2
         1  -5
         9  NXPORD

DEST2    4  STODEV (RESET STANDARD OUTPUT DEVICE )

         8  DEST1

SRCE     5  SP
         0  W
         /4 0
SRCE1    0  W10
         /5 GLIDEV
         9  SRCE2
         7  SRCE2
         1  -4
         9  NXPORD
SRCE2    4  +1
         8  SRCE1



DIGIT    5  SP
         0  W
         /4  0
DIGIT1   0  W10
         /5 GLINTD
         1  -1
         9  DIGIT2
         1  -12
         9  NXPORD
DIGIT2   4  +6
         8  DIGIT1




(903 ALG INT INPUT AND OUTPUT PT2 )

DIVTEN   0    W4       ( SUBRT DIVIDE FRACTION )
         14 1          (  BY 10  OR  100*2↑-3  )
         4    W3
         14   8188     (FORM F*2↑-4 )
         13 TENPWR (DIVIDE BY 10*2↑-4 OR 100*2↑-7)

         14   18
         5    SBW
         14   3        ( QA*2↑3  OR QA*2↑6  )
         12   TENPWR
         14   1        ( QA*10 OR QA*100*2↑3 )
         3    SBW2
         5    SBW1
         4    SBW2     ( FORM 10*DQ =F-QA*10 )
         2    W4
         9    ;+2      ( SUBTRACT LEAST SIGNIFICANT )
         8    ;+3
         10   SBW1     ( CARRY DIGIT  )
         6    =15 8191
         5    SBW2
         4    SBW1
         2    W3       ( SUBTRACT MOST SIG )
         5 SBW1
         0    SBW2
         14   1
         4    SBW1     ( DQ*2↑11 )
         14   11
         13   TENPWR   (    )
         0    +0
         14   8177     (FORM DQ )
         3    W4       (  TRUE QUOTIENT =QA+DQ )
         1    SBW
         5    W3
         0    SBLNK    (  EXIT  )
         /8   1


DECML5   4    +4
         1    W5
         5    W5       (EXPONENT +4 )

DECML    0    W4
         14   1        ( SCALE  F*2↑-4 )
         4    W3
         14   8188
         3    W4
         5    W3

DECML1   0    W4
         14   1
         4    W3
         14   1        ( F*2 )
         3    W4
         5    W3
         14   2        ( F*8 )
         3    SBW

DECML4   1    W3       ( ADDN ENTRY ALSO 10F=F*8+F*2 )
         5    W3
         4    SBW
         1    W4       (ADD L S )
         9    DECML2
DECML3   5    W4
         0    SBLNK    ( EXIT )
         /8   1

DECML2   6 =15 8191 ( CARRY BIT  )
         10   W3
         8    DECML3



(  OUTPUT )
OUTR     5  SP
         4  ODEV1
         5  ODEV
         4  DIGN1
         5  DIGN
         4  DIGM1
         5  DIGM
         4  OPMD1
OUTR2    5  OPMD
         11 SBLNK
         8  W345R1+1 ( COPY FROM STACK INTO W3 W4 W5)
         4  +4
         8  OINT2


OUTI     5  SP
         4  ODEV1
         5  ODEV
         0  W
         /4 0
OUTI4    5  W3
         4  INTDG
OUTI5    5  DIGM
         4  +0
OINT2    5  ADPART
         4  LDZERO
         5  NSIGNF
         4  +0
         5  SIGNCH

         4  PRFA1
         11 SBLNK2 ( OUTPUT PREFIX )
         8  OSTSB
         4  ADPART
         7  OUTI3
OINT3

         4  +0
         5  DECPWR
         4    W3
         9    OPRL1

                       (This instruction was 14 1)
         1    W3       (in earlier issues, but this)
                       (gave ERROR 7 when used to)
                       (print zero on a 920M)

         9    OPRL3
         7    OPRL19   (TEST ZERO )
OPRL2    4    FLNS6    (NON STANDARD FP NUMBER )

         11 PAUSRT
         8  FAILEN
         4  +10
         11 OTCHA
         8  OTCHA1
         11 OTCHA
         8  OTCHA2
         11 OTCHA
         8  OTCHA2
         8  INOEX


OPRL1    14   1
         9    OPRL2    (NON STANDARD )

         0  W3ADDR
         11 SBLNK ( NEGATE )
         8  NEGR6
         4    +13
         5    SIGNCH   (SET TO PRINT MINUS )
OPRL3
OPRL4    4    W5
         9    OPRL12   ( JUMP IF EXP NEG )
         4    &310000  ( SET DIVISOR 100*2↑-7 )
         5    TENPWR
OPRL5    4    W5
         9    OPRL10
         7    OPRL11   (JUMP IF POWER CORRECT )
         4    +5
         2    W5
         9    OPRL8    (IF EXP < 5 JUMP )
         1    +2
         5    W5       ( ADJUST SCALE )
         10   DECPWR
OPRL9    11   SBLNK
         8    DIVTEN   ( DIVIDE BY 10 OR 100*2↑-3 )
         10   DECPWR

         11   STAND
         8    STAND+1
         8    OPRL5

OPRL8    4    =10 0
         5    TENPWR   ( SET DIVISOR 10*2↑-4 )
         8    OPRL9

OPRL18   10 DECPWR
         4 &031463 ( SET 0.1*❿(N+1)
         5  W3
         4  &063160
         5  W4
         8  OPRL20
OPRL19   10 DECPWR

         4    W4
         7    OPRL20
         8    OPRL2    ( GO TO FAIL NON STANDARD )

OPRL13   4    &314631  ( IF EXP=-3 )
         2    W3
         9    OPRL14   ( TEST MS )
         7    ;+2
         8    OPRL10   ( IF > .800001 OK )
         4    &231463
         2    W4
         9    OPRL14   ( TEST EXACT )
         8    OPRL10

OPRL14   4    W5       ( EXP:=EXP+4 )
         1    +4
         5    W5
         11   SBLNK    ( MULT BY 10*2↑-4 )
         8    DECML
         11   STAND
         8    STAND+1
OPRL15   4    +1
         2    DECPWR
         5    DECPWR
OPRL12   4    W5
         1    +3
         9    OPRL14   ( EXP>-3 )
         7    OPRL13   ( EXP=-3 )
OPRL10   4    W5
         11   SBLNK    ( *2↑EXP )
         8    SHIFTD

OPRL11   4  OPMD
         7  ;+4 (FREEPOINT )
         9  ;+6 ( ALIGNED )
         4  DIGN
         8  ;+6
         4  DECPWR
         9  ;+3
         8  ;-4
         4  DECPWR
         1  DIGN
         9  OPRL20
         5  SBW1 ( STORE TOTAL SIGNIF DIGITS )
         1  SBW1
         2  +0
         5  SBW
         0  SBW1
         /4 RNDTBL
         0  SBW
         /14  0
         3  SBW
         11  SBLNK
         8   DECML4 ( ADD IN ROUNDING )
         4  W3
         9  OPRL18

OPRL20   4    OPMD     (WHAT FORMAT )
         7    OPRL21   (FREEPOINT )
         9    OPRL22   ( ALIGNED )
         4    +1       ( SCALED )
         2    DIGN
OPRL25   5    AFTPT
         4    +1
         5    BEFPT

         11   SBLNK1
         8    OTMX1
         4    +16
         5    NSIGNF
         4    +11
         5    SIGNCH

         4  +31
         11 OTCHA
         8  OTCHA1 ( PRINT ❿ )

         4  DECPWR
         1  -1
         5  W3
         4  +2
         5  DIGM
         4  +0
         5  ADPART
OUTI3    5  W4
         5  DIGN

         4  -1
         5  OPMD
         4  +17
         5  W5
         11  STAND
         8   STAND+1
         8 OINT3

OPRL27   4    +0
         8    OPRL28

OPRL21   4    DECPWR
         9    OPRL27
OPRL28   5    BEFPT
         2    DIGN
         9    OPRL24
OPRL23   5    AFTPT

         11   SBLNK1
         8    OUTMXD

         8  INOEX


OPRL22   4  DECPWR
         2  DIGM
         9  OPRL29 ( ALARM  PRINT REQUD )
         4  DIGM
         5  BEFPT
         4  DIGN
         8  OPRL23



OPRL29   4  DIGM
         1  DIGN
         5  DIGN
OPRL24   4  ADPART
         6  +8191
         7  ;+2 ( IF INTEGER PRINT 1 LESS )
         4  +1
         1  DIGN
         1  -6
         (NO ROOM FOR NORMAL ALARM PRINT )
         9  OPRL26
         1  +1
         5  DIGN
         4  +1
         5  OPMD (SET SCALED FORMAT )
         5  ADPART
         8  OPRL11

OPRL26
         2  -6
         11 OTCHA
         5  SW
         4  SW
         10 SW
         7  ;+3
         4  +0 ( OUTPUT N-1 SPACES )
         8  OTCHA1
         4  +40
         11 OTCHA
         8  OTCHA1 ( PRINT H )

         11 SBLNK
         8  FINSUB
         8 INOEX


OUTMXD   4    DECPWR   (SUBRT OUTPUT MIXED NUMBER)
OTMX1    5    SW1
OTMX2    4    BEFPT
         7    OTMX4    ( NO MORE DIGITS BEFORE POINT)
         1    AFTPT
         7    OTMX13   ( ALL DIGITS PRINTED )
OTMX3    4    SW1
         2    BEFPT
         9    OTMX7
         7    OTMX6
         4    BEFPT
         1    -1
         7    OTMX11
         4    NSIGNF
         5    SW       ( SET CHARACTER )
         7    OTMX10   ( IF SPACE OUTPUT )
         8    OTMX9    (OTHERWISECHECK IF SIGN PRINTED)

OTMX11   4    +16
         5    SW
         8    OTMX9

OTMX4    4    AFTPT
         7    OTMX12
         4    +16
         5    NSIGNF
         4    SIGNCH
         9    OTMX5    ( IF SIGN NOT OUTPUT )
         11   OTCHA
         8    OTCHA1   ( OUTPUT SIGN )
         4    -1
         5    SIGNCH
OTMX5    4    +14
         11   OTCHA    ( OUTPUT POINT )
         8    OTCHA1
         8    OTMX3

OTMX6    11   SBLNK
         8    DECML    ( F*2↑-4*10 )
         8    OTMX8

OTMX7    11   SBLNK
         8    DECML1   ( F*10 )
OTMX8    4    W3
         14   8179
         1    +16
         5    SW       ( SET CHAR = DIGIT PART )
         4    W3
         6    +8191    ( LEAVE FRACTIONAL )
         5    W3       ( PART IN STORE )
OTMX9    4    SIGNCH
         9    OTMX10
         11   OTCHA
         8    OTCHA1
         4    -1
         5    SIGNCH
OTMX10   4    SW
         11   OTCHA
         8    OTCHA1   ( OUTPUT CHAR )
         4    BEFPT
         1    -1       ( REDUCE NO OF CHARS  )
         5    BEFPT
         8    OTMX2

OTMX12   4    ADPART
         7    OTMX13
         4    +0
         11   OTCHA    ( IF REAL NO .  PRINT  )
         8    OTCHA1   ( SPACE INSTEAD OF POINT )
OTMX13   0    SBLNK1   (EXIT)
         /8   1


RDNM26   4  FLNS19
         11 PAUSRT
         8  FAILEN
RDNM     4    +0
         5    W1       ( DIGIT TOTAL :=0)
         5    W2       (SIGN FLAG )
         5    W3       ( NUMBER:=0 )
         5    W4
         5    W5
         5    W6       (REAL NUMBER FLAG)
         5    W7       ( EXP FLAG )

         5    W9       ( DECIMAL EXP:=0 )
         5    WS20
         4    =/0 0
         5  W8 (SET NO DIGITS AFTER POINT )
         8  ;+5

         4  WS22
         5  W2
RDNM1    11 GECHAR
         8  GECHEN
         0  IDEV
         /4 BUFFER-1
         5  WS21
         0  WS21
         /4 TABLE
         6    =8 0
         7    RDNM3    (JUMP IF NOT DIGIT )
         10   W1       (TOTAL DIGITS )
         10   WS20     ( SET SIGN NOT NULL)
         4    W7
         7    RDNM2    (JMP IF EXP NOT SET)
         0    +0
         4    W9
         1    W9       (DECIMAL EXP*10 )
         5    W9
         14   2
         1    W9
         1    WS21
         1    -16      (ADD IN DIGIT )
         5    W9



         2  +28
         9  ;+2 (IF DECIMAL EXPT>28 SET=28 )
         8  RDNM1 ( AS DEC EXPT>28 NOT SIGNIFICANT )
         1  W9 ( NUMBER < 2❿-19 GIVES ZERO )
         8  ;-5 ( NUMBER>5❿+19 GIVES OVERFLOW )



RDNM2    10   W8
         11   SBLNK    (NUMBER*10*2↑-4)
         8    DECML5
         4    W5
         2    +17
         6    +8191    ( SCALING )
         1    SHFT1
         5    ;+4
         4    WS21
         1    -16      ( SCALE DIGIT )
         0    +0
         +0
         3    SBW
         11   SBLNK    ( ADD TO NUMBER )
         8    DECML4
         11   STAND
         8    STAND+1
         8    RDNM1


RDNM5    4    W6       (IS REAL SET )
         10   W6
         7    RDNM6
RDNM7    4    FLNS17   ( ILLEGAL CHAR )


         8  RDNM26+1


RDNM6    5    W8       (START COUNT FOR DIGITS  )
         8    RDNM1    (   AFTER POINT  )

RDNM8    4    +1       (SET -VE  SIGN )
RDNM9    5    WS22     (SET +VE )
         4  WS20
         7  RDNM1-2
         8  RDNM27

RDNM10   5  WS20
         4  W7
         7    RDNM11   (IF EXP SET FAIL )
         8    RDNM7
RDNM11   10   W7       ( SET EXP )
         4    W1
         7    RDNM12   (JUMP IF NO DIGITS YET )
RDNM13   4    W2       ( STORE SIGN )
         5    W10
         4    +0
         5    W2       ( CLEAR SIGN )
         10   W6       (SET REAL )
         8    RDNM1


RDNM12   4    =8 0
         5    W3       (SET +1 IN NUMBER)
         4    +1
         5    W5
         10   W1       ( COUNT ONE DIGIT )
         8    RDNM13

RDNM3    4    WS21
         1    -14
         7    RDNM5    ( POINT )
         1    -17
         7    RDNM10   ( ❿ )
         1    +18
         7    RDNM8    ( - )
         1    +2
         7    RDNM9    ( + )
         2    +21

         7  RDNM26
RDNM27   4    W1       ( TERMINATOR )
         7    RDNM1    (IF NO DIGITS YET IGNORE )
         4    W7
         7    RDNM14   (EXP NOT SET )
         4    W2
         7    RDNM4    (SIGN OF EXP )
         4    W9
         2    +0
         5    W9
RDNM4    4    W10      (RESTORE SIGN )
         5    W2
RDNM14   4    W8
         9    RDNM15   (JUMP IF NO DIGITS AFTER PT )

2        W9
5        W9   ( SUBTRACT NUMBER FROM DEC EXP )
RDNM15   4    W9
         7    RDNM20   (DEC EXP ZERO )
         9    RDNM16   (       <0   )
         1    -1       ( IF >0 REDUCE )
         5    W9
         11   SBLNK
         8    DECML5   (  MULTIPLY BY TEN )
RDNM19   11   STAND
         8    STAND+1
         8    RDNM15


RDNM16   10   W9
         4    =10 0
         5    TENPWR   ( SET DIVISOR =+10 )
         11   SBLNK
         8    DIVTEN   ( DIVIDE )
         8    RDNM19



RDNM20   4  W2
         7  ;+4
         0  W3ADDR
         11 SBLNK
         8  NEGR6
         4  ADPART
         2  +2
         7  ;+3
         11 SBLNK1
         8  RICONV ( CONVERT TO INTEGER FORM)
         4  W
         5  SP
         5  PKDADD
         11 STSUBRT
         8  STSUB
         8  INOEX


         0  SBLNK2 (EXIT )
         /8 1

OSTSB    5 WS2 ( ADDRESS )
         4  -12
         5  WS3 ( CHAR. POSITION )
         4  -1
         5  WS6 ( N ; SET TO NULL )
         5  WS4 ( STRING DEPTH )

OST2     0 WS2
         /4 0
         0 WS3
         /14 0
         6  +63
         5  WS5 ( CHARACTER )
         4  WS3
         7  OST1
         1  +6
OST3     5  WS3
         4  WS4
         9  OST4 (BEGINNING OF STRING )
         7  OST6 ( ORDINARY STRING DEPTH )
         0  WS5 ( INNER STRING )
         /4  TABLE
         6  =8 0
         7  OST5 (JUMP IF NOT DIGITS )
         4  WS6
         12  +10 ( N*10 )
         14 17
         1  WS5 ( ADD DIGIT )
         1  -16
         5  WS6
         8  OST2


OST1     4  -12 (RESET CHAR. POSN. )
         10 WS2 ( ADVANCE ADDRESS )
         8  OST3

OST4     4  WS5
         1  -7
         7  OST2-1 ( IF OPQT SET ORD. STRING)
         4  +23 ( STRING MUST BEGIN WITH ' )
         8  ENFAIL

         4  +1
         8  OST2-1 ( SET INNER STRING )

OST6     4  WS5
         1  -7
         7  OST6-2 ( OPENQT )
         1  -25
         7  OSTSB-2 ( IF CLOSEQT EXIT )
         4  WS5
         11 OTCHA
         8  OTCHA1 ( OUTPUT CHAR. )
         8  OST2


OST5     4  WS6
         9  OST9+3 ( NO OUTPUT REQUD. )
         4  WS7
         1  -34
         7  OST9-3

         4  WS6
         1  -2
         5  WS6
         9  OST9
         4  WS14
         11 OTCHA
         8  OTCHA1
         4  WS6
         1  -1
         8  ;-7

         4  =/0 0
         8 ;+2 ( RUNOUT  CHAR )
OST8     4  HALTCH
         8  OST8+8
         4  +1 ( NEWLINE FOR LAST CHAR.)
         5  WS7
         4  LFCH+1 ( CHAR. FOR REPEATED NEWLINE)
         8  OST8+9 ( I.E. LINE FEED ON TELEPRINTER )
         1  +25
         1  +7
         5  WS7 ( STORE CHARS. FOR OUTPUT )
         5  WS14
         4  +0 ( SET N TO ZERO )
         5  WS6
         8  OST2


         4  WS6
         1  =/0 0
         8  ;+2 ( OUTPUT BINARY CODE VALUE )
OST9     4  WS7
         11 OTCHA ( OUTPUT FINAL CHAR.)
         8  OTCHA1
         4  WS5
         5  WS7 ( PREVIOUS CHAR )
         1  -32 ( CLOSE QT )
         7  OST2-1 ( SET ORDINARY STRING DEPTH )
         1  -2
         7  OST8+10 ( B )
         1  -6
         7  OST8 ( H )
         1 -4
         7  OST8+2 ( L )
         1  -5
         7  OST8+7 ( Q )
         1  -1
         7  OST8-2 ( R )
         1  -1
         7  OST8+8 ( S )
         1  -2
         7  OST8+6 ( U )
         4  FLNS7 ( ILLEGAL CHAR )
         11 PAUSRT
         8  FAILEN
         8  OST8+2

<! Halt !>