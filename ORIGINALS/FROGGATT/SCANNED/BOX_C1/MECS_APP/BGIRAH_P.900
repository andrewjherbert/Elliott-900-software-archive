<! Legible Header OOO    OO  OOOOO OOOO   OOO  O   O       OOOO  OOOO   OOO    OO  OOOO   OOO  O   O        OOO     O  OOOOO  OOO  OOOOO  !>
<! Legible Header    O  O OO   O   OOOOO O   O O   O       O   O OOOOO O   O  O OO OOOOO O   O OO OO       O   O  OOO      O OOOOO     O  !>
<! Legible Header    O O       O   O   O O   O O   O       O   O O   O O   O O     O   O O   O OOOOO           O  OOO     O  O        O   !>
<! Legible Header OOO  O       O   OOOOO OOOOO OOOOO       OOOO  OOOOO O   O O     OOOOO OOOOO O O O         OOO    O    O   OOOO    O    !>
<! Legible Header    O         O   OO    O   O O   O       O     OO    O   O       OO    O   O O   O                O        O   O        !>
<! Legible Header    O O  OO   O   O O         O   O       O     O O   O   O O  OO O O         O   O       OO       O   O    O   O  O     !>
<! Legible Header OOOO  O  O   O   O  O  O   O O   O       O     O  O  O   O  O  O O  O  O   O O   O       O        O        O   O        !>
<! Legible Header OOOO   OOO OOOOO O   O O   O O   O       O     O   O  OOO    OOO O   O O   O O   O       OOOOO    O  O      OOO  O      !>

BGIRA HEAT TRANSFER PROGRAMME FOR A CYCLE OF 15 SECONDS;
"BEGIN"
"REAL" RHO,C,K,RHODASH,CDASH,KDASH,SLICE,HTCM1,HTCM2,HTCGM,EMOULD,AIRTEMP,TIME,
TSM1,TSG1,TSG2,TSM2,SUM1,SUM2,SUM3,RCS,RCS1,W1,W2,RCSS1,RCS2;
"INTEGER" BAND,I,J,N,L;
"ARRAY" ICF[1:3,0:10,0:10],ECF[1:3,0:10],M1CF,M2CF[1:13],T[1:47];

"SWITCH" SSS:=PRINTROUTINE,EXIT1,EXIT2,FINISH;
"BOOLEAN" GLASSIN;
"COMMENT" BLACK BODY RADIATION PROCEDURES;
   "REAL""PROCEDURE" BB1G(TEMP);
          "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
      X:=0.01*TEMP-9; "COMMENT" SCALING OF TEMPERATURE;
         BB1G:=(((1.93518353?-4*X+6.65421745?-3)*X+6.73988527?-2
       )*X+2.95612719?-1)*X+4.92037607?-1;
"END" BB1G;
   "REAL""PROCEDURE" BB2G(TEMP);
   "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
   X:=0.01*TEMP-9; "COMMENT" SCALING OF TEMPERATURE;
   BB2G:=((3.30623514?-4*X+1.81444868?-2)*X
           +1.43098546?-1)*X+0.338940535;
"END" BB2G;
   "REAL""PROCEDURE" BB3G(TEMP);
   "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
    X:=0.01*TEMP-9; "COMMENT" SCALING OF TEMPERATURE;
    BB3G:=(0.020601299*X+0.255490198)*X+0.80933915;
"END" BB3G;
    "REAL""PROCEDURE" BB4G(TEMP);
    "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
    X:=0.01*TEMP-9; "COMMENT" SCALING OF TEMPERATURE;
    BB4G:=5.57840675?-3*X*X+1.78229593?-1*X+9.25612285?-1;
"END" BB4G;










    "REAL""PROCEDURE" BB1M(TEMP);
    "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
    X:=0.01*TEMP-9; "COMMENT" SCALING OF TEMPERATURE;
    BB1M:=((((8.91050252?-6*X+4.74958139?-4)*X+
       8.70166876?-3)*X+7.45259395?-2)*X+3.08418163?-1
      )*X+5.01468905?-1;
"END" BB1M;
    "REAL""PROCEDURE" BB2M(TEMP);
    "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
   X:=0.01*TEMP-9;
                BB2M:=((((-1.25665290?-5*X-2.63658051?-4)*X-
            9.97377356?-4)*X+1.42850961?-2)*X+
              1.36275415?-1)*X+3.33967813?-1;

"END" BB2M;
    "REAL""PROCEDURE" BB3M(TEMP);
    "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
    X:=0.01*TEMP-9;
    BB3M:=(((-1.33309611?-4*X-1.20645902?-3)*X
                +1.77238807?-2)*X+2.52105084?-1)*X+8.0661101?-1;
"END" BB3M;
    "REAL""PROCEDURE" BB4M(TEMP);
    "VALUE" TEMP; "REAL" TEMP;
"BEGIN""REAL" X;
    X:=0.01*TEMP-9;
    BB4M:=1.08458951?-2*X*X+2.05189912?-1*X+9.63898643?-1;
"END" BB4M;









"PROCEDURE" FCT(T,TSM1,TSG1,TSG2,TSM2,GLASSIN,DERIV);
"REAL" TSM1,TSG1,TSG2,TSM2;
"ARRAY" T,DERIV;
"BOOLEAN" GLASSIN;
"BEGIN""REAL" X,Y,Z;
"INTEGER" L,N,M,J,I;
"ARRAY" HC[1:47];
"SWITCH" SW:=GIN,GOUT;
"IF" GLASSIN "THEN" "GOTO" GIN "ELSE" "GOTO" GOUT;
"COMMENT" CALCULATION OF RADN. INTERCHANGE BETWEEN GLASS LAYERS;
GIN:
"FOR" L:=18 "STEP" 1 "UNTIL" 30 "DO" HC[L]:=0;
"FOR" M:=0 "STEP" 1 "UNTIL" 10 "DO"
    "BEGIN""SWITCH" S:=REPEAT;
L:=M+19;
X:=BB1G(T[L]);
Y:=BB2G(T[L]);
Z:=BB3G(T[L]);
"FOR" N:=0 "STEP" 1 "UNTIL" 10 "DO"
           "BEGIN""IF" N=M "THEN""GOTO" REPEAT;
      W1:=X*ICF[1,M,N]+Y*ICF[2,M,N]+Z*ICF[3,M,N];
          HC[N+19]:=HC[N+19]+W1;
          HC[L]:=HC[L]-W1;
REPEAT: "END";
   "END";
"COMMENT" RADN. FROM AND TO MOULD;

W1:=0.96*EMOULD*(BB4M(TSM1)-BB4G(TSG1));
HC[18]:=HC[18]-W1;
HC[19]:=HC[19]+W1;

X:=BB1M(TSM1);

Y:=BB2M(TSM1);
Z:=BB3M(TSM1);
W1:=M1CF[1]*X+M1CF[2]*Y+M1CF[3]*Z;HC[30]:=HC[30]+W1;
HC[18]:=HC[18]-W1;

"FOR" L:=19 "STEP" 1 "UNTIL" 29 "DO"
    "BEGIN" I:=L-19;
W1:=ECF[1,I]*X+ECF[2,I]*Y+ECF[3,I]*Z;
HC[L]:=HC[L]+W1;
HC[18]:=HC[18]-W1;
    "END";
"FOR" L:=19 "STEP" 1 "UNTIL" 29 "DO"
      "BEGIN"
X:=BB1G(T[L]);
Y:=BB2G(T[L]);
Z:=BB3G(T[L]);
J:=L-19;
I:=29-L;



W1:=ECF[1,J]*X+ECF[2,J]*Y+ECF[3,J]*Z;
W2:=ECF[1,I]*X+ECF[2,I]*Y+ECF[3,I]*Z;
HC[18]:=HC[18]+W1;
HC[30]:=HC[30]+W2;
HC[L]:=HC[L]-W1-W2;
       "END";
W1:=0.96*EMOULD*(BB4M(TSM2)-BB4G(TSG2));
HC[29]:=HC[29]+W1;
HC[30]:=HC[30]-W1;
X:=BB1M(TSM2);
Y:=BB2M(TSM2);
Z:=BB3M(TSM2);
W1:=M2CF[1]*X+M2CF[2]*Y+M2CF[3]*Z;HC[18]:=HC[18]+W1;
HC[30]:=HC[30]-W1;









"FOR"L:=19 "STEP" 1 "UNTIL" 29 "DO"
          "BEGIN"
J:=29-L;


W1:=ECF[1,J]*X+ECF[2,J]*Y+ECF[3,J]*Z;
HC[L]:=HC[L]+W1;
HC[30]:=HC[30]-W1;
          "END";
"COMMENT" PLANE CONDUCTION---GLASS IN, AND CONVERSION TO DEGC/SEC.
HEAT BALANCE EQNS FORMED GIVING DT BY DT;
GOUT:
DERIV[1]:=(HTCM1*(AIRTEMP-T[1])-KDASH*(T[1]-T[2])/SLICE)/RCS2;
"FOR" L:=2 "STEP" 1 "UNTIL" 16 "DO"
DERIV[L]:=(T[L-1]-2*T[L]+T[L+1])*KDASH/RCSS1;
DERIV[17]:=(T[16]-2*T[17]+TSM1)*KDASH/RCSS1;
"IF" GLASSIN "THEN"
"BEGIN" DERIV[18]:=(HC[18]+HTCGM*(TSG1-TSM1)-KDASH*(TSM1-T[17])/SLICE) /RCS2;
DERIV[19]:=(HC[19]+HTCGM*(TSM1-TSG1)-K*(TSG1-T[20])/SLICE)
       /(RCS*0.5);
DERIV[20]:=(HC[20]+(TSG1-2*T[20]+T[21])*K/SLICE)/RCS;

"FOR" L:=21 "STEP" 1 "UNTIL" 27 "DO"
DERIV[L]:=(HC[L]+(T[L-1]-2*T[L]+T[L+1])*K/SLICE)/RCS;
DERIV[28]:=(HC[28]+(T[27]-2*T[28]+TSG2)*K/SLICE)/RCS;
DERIV[29]:=(HC[29]+HTCGM*(TSM2-TSG2)-K*(TSG2-T[28])/SLICE) /(RCS*0.5);
DERIV[30]:=(HC[30]+HTCGM*(TSG2-TSM2)-KDASH*(TSM2-T[31])/SLICE)/RCS2;
"END"
"ELSE"
"BEGIN" DERIV[18]:=(HTCM1*(AIRTEMP-TSM1)-KDASH*(TSM1-T[17])/SLICE)/RCS2;
DERIV[30]:=(HTCM2*(AIRTEMP-TSM2)-KDASH*(TSM2-T[31])/SLICE)/RCS2;
"END";
DERIV[31]:=(TSM2-2*T[31]+T[32])*KDASH/RCSS1;

"FOR" L:=32 "STEP" 1 "UNTIL" 46 "DO"
DERIV[L]:=(T[L-1]-2*T[L]+T[L+1])*KDASH/RCSS1;
DERIV[47]:=(HTCM2*(AIRTEMP-T[47])-KDASH*(T[47]-T[46])/SLICE)/RCS2;
"END";




"PROCEDURE" KUTTAMERSON(N,TIME,T,EPS,INT,OUTPUT,TSM1,TSG1,TSG2,TSM2);
"VALUE" N,INT,EPS; "INTEGER" N; "REAL" TIME,EPS,INT,TSM1,TSG1,TSG2,TSM2;
"ARRAY" T;
"LABEL" OUTPUT;
"BEGIN"
"INTEGER" I,LOC,PLOC;
"REAL" ERROR,T1SM1,T1SG1,T1SG2,T1SM2;
"ARRAY" T1,T2,DERIV,DERIV1,DERIV2[1:N];
"PROCEDURE" UPDATE;
"BEGIN"T1SM1:=1.3333333*T1[18]-0.3333333*T1[17];
       T1SG1:=1.3333333*T1[19]-0.3333333*T1[20];
       T1SG2:=1.3333333*T1[29]-0.33333333*T1[28];
       T1SM2:=1.3333333*T1[30]-0.3333333*T1[31];
"END" UPDATE;"BOOLEAN" INCREASE;
"SWITCH" SS:=NEXT;
PLOC:=1; LOC:=0;
NEXT:
FCT(T,TSM1,TSG1,TSG2,TSM2,GLASSIN,DERIV);
"FOR" I:=1 "STEP" 1 "UNTIL" N "DO"
T1[I]:=T[I]+INT/3*DERIV[I];



<! Halt !>






"COMMENT" PAGE 4;
UPDATE;
FCT(T1,T1SM1,T1SG1,T1SG2,T1SM2,GLASSIN,DERIV1);
"FOR" I:=1 "STEP" 1 "UNTIL" N "DO" T1[I]:=T[I]+INT/6*(DERIV[I]+DERIV1[I]);
UPDATE;

FCT(T1,T1SM1,T1SG1,T1SG2,T1SM2,GLASSIN,DERIV1);
"FOR" I:= 1 "STEP"1 "UNTIL" N "DO" T1[I]:=T[I]+INT/8*(DERIV[I]+3*DERIV1[I]);

UPDATE;

FCT(T1,T1SM1,T1SG1,T1SG2,T1SM2,GLASSIN,DERIV2);
"FOR" I:=1 "STEP" 1 "UNTIL" N "DO" T1[I]:=T[I]+INT/2*DERIV[I]-3*INT/2*DERIV1[I]
      +2*INT*DERIV2[I];
UPDATE;

FCT(T1,T1SM1,T1SG1,T1SG2,T1SM2,GLASSIN,DERIV1);
"FOR" I:=1 "STEP" 1 "UNTIL" N "DO"  T2[I]:=T[I]+INT/6*DERIV[I]+2*INT/3*DERIV2[I]
      +INT/6*DERIV1[I];

UPDATE;

INCREASE:="TRUE";
"FOR" I:=1 "STEP" 1 "UNTIL" N "DO"
              "BEGIN"
ERROR:=ABS(0.2*(T1[I]-T2[I]));
"IF" ERROR>EPS "THEN"
                       "BEGIN"
INT:=INT/2;
PLOC:=2*PLOC;
LOC:=2*LOC;
"GOTO" NEXT
                       "END";
"IF" ERROR*32>EPS "THEN" INCREASE:="FALSE";
              "END" I;
"COMMENT" IGNORE ERROR IN SURFACE TEMPS AS THEY ARE FOUND BY EXTRAPOLATION
              OF OTHER RESULTS;









TIME:=TIME+INT;
"FOR" I:=1 "STEP" 1 "UNTIL" N "DO" T[I]:=T2[I];
TSM1:=T1SM1;   TSG1:=T1SG1;   TSG2:=T1SG2;   TSM2:=T1SM2;
LOC:=LOC+1;
"IF" LOC=PLOC "THEN""GOTO" OUTPUT;
"IF" INCREASE "AND" LOC=LOC "DIV" 2*2 "AND" PLOC>1 "THEN"
              "BEGIN"
INT:=2*INT;
LOC:=LOC"DIV" 2;
PLOC:=PLOC"DIV" 2
              "END";
"GOTO" NEXT "END";
"COMMENT" ALL PROCEDURES NOW IN. START OF PROGRAMME PROPER;


"FOR" BAND:=1 "STEP" 1 "UNTIL" 3 "DO"
              "BEGIN""FOR" N:=1 "STEP" 1 "UNTIL" 30 "DO"
                            "BEGIN""READ" I,J,ICF[BAND,I,J];
            ICF[BAND,J,I]:=ICF[BAND,10-J,10-I]:=ICF[BAND,10-I,10-J]:=
                  ICF[BAND,I,J]
                            "END";
              "FOR" J:=0 "STEP" 1 "UNTIL" 10 "DO""READ" ECF[BAND,J];
                   "READ" M1CF[BAND],M2CF[BAND]
              "END";
"READ" RHO,C,K,RHODASH,CDASH,KDASH,HTCM1,HTCM2,HTCGM,SLICE,AIRTEMP,TIME,EMOULD,
TSM1,TSG1,TSG2,TSM2;
"FOR" L:=1 "STEP" 1 "UNTIL" 47 "DO""READ" T[L];
"COMMENT" ALL DATA NOW IN;




RCS:=RHO*C*SLICE;
RCS1:=RHODASH*CDASH*SLICE;
RCSS1:=RCS1*SLICE;
RCS2:=RCS1/2.0;




GLASSIN:="TRUE";
PRINTROUTINE:
"PRINT" ''L2@@,ALIGNED(2,4),TIME,''L@@; ALIGNED(4,2);
SUM1:=-0.5*(T[1]+T[18]);
"FOR" L:=1 "STEP" 1 "UNTIL" 18 "DO"
    "BEGIN"
"PRINT" T[L];
SUM1:=SUM1+T[L]
   "END";
"PRINT" TSM1;









"IF" GLASSIN "THEN"
"BEGIN"
"PRINT" ''L@@,TSG1;
SUM2:=-0.5*(T[19]+T[29]);
"FOR" L:=19 "STEP" 1 "UNTIL" 29 "DO"
    "BEGIN"
"PRINT" T[L];
SUM2:=SUM2+T[L];
    "END";
"PRINT" TSG2;
"END"
"ELSE""PRINT" ''L6@ GLASS OUT'L5@@;
"PRINT" ''L@@,TSM2;
SUM3:=-0.5*(T[30]+T[47]);
"FOR" L:=30 "STEP" 1 "UNTIL" 47 "DO"
    "BEGIN"
"PRINT" T[L];
SUM3:=SUM3+T[L]
    "END";
"PRINT" ''L2@@,SAMELINE,SCALED(6),''L2@HEAT CONTENT:-
'LS3@TOP MOULD=@,SUM1*RCS1;
"IF" GLASSIN "THEN"
"PRINT" ''LS7@GLASS=@,SUM2*RCS;
"PRINT" ''L@BOTTOM MOULD=@,SUM3*RCS1;
"IF" TIME+?-6<1.0 "THEN""GOTO" EXIT1
"ELSE""IF" TIME+?-6<5.0 "THEN""GOTO" EXIT2
"ELSE""IF" TIME+?-6<6.0 "THEN"
              "BEGIN" GLASSIN:="FALSE";
                     "GOTO" EXIT1
              "END"
"ELSE""IF" TIME+?-6<15.0 "THEN""GOTO" EXIT2
"ELSE""GOTO" FINISH;
"COMMENT" END OF PRINT ROUTINE;
EXIT1:KUTTAMERSON(47,TIME,T,0.015625,0.1,PRINTROUTINE,TSM1,TSG1,TSG2,TSM2);
EXIT2:KUTTAMERSON(47,TIME,T,0.015625,1.0,PRINTROUTINE,TSM1,TSG1,TSG2,TSM2);
"COMMENT" EXIT1 GIVES TIME STEP OF 0.1 SECONDS,EXIT2 GIVES TIME STEP OF 1SECOND;
FINISH:"END";



<! Halt !>