
O 2
M

*SETDIC 1500 50
*NOMEM

[SINL SINE COSL COSE CHOPC CHOPF CHOPL CHOPE QOUT1]

*DEFINE WORKSPACE (X) [X >1]

*DEFINE START [<]
*DEFINE STOP [8 ;+0]

*DEFINE OPENOUTPUT [4 +1 5 CHOPC 4 +0 5 CHOPF]
*DEFINE CLOSEOUTPUT [4 +10 11 CHOPL 8 CHOPE 4 +20 11 CHOPL 8 CHOPE]
*DEFINE ONNEWLINE [4 +10 11 CHOPL 8 CHOPE]
*DEFINE ONSAMELINE [4 +9 11 CHOPL 8 CHOPE]
*DEFINE PRINT (X) [ONSAMELINE 4 X 11 QOUT1 8 QOUT1+1 +0]

*DEFINE PUT (X) [4 X]
*DEFINE INTO (X) [5 X]
*DEFINE ADD1TO (X) [10 X]
*DEFINE ADD (X) [4 X]
*DEFINE TO (X) [1 X 5 X]
*DEFINE GOTO (X) [8 X]

*DEFINE IF (X) [4 X]
*DEFINE OVER (X) [2 X 9 ;+2]
*DEFINE UNDER (X) [2 X 2 +0 9 ;+2]
*DEFINE BEGIN [8 L*$]
*DEFINE END [L*$]

*DEFINE MULTIPLEOF2TO (X) [0 +0 14 P18-X 7 ;+2] P18=18

*DEFINE PRINTFUNCTION (X) [12 +.318310 14 10 X 12 +2560
                           5 SCRAT1 ONSAMELINE 4 SCRAT1
                           11 QOUT1 8 QOUT1+1 +1] SCRAT1 >1
*DEFINE PRINTSIN (X) [4 X PRINTFUNCTION (11 SINL 8 SINE)]
*DEFINE PRINTCOS (X) [4 X PRINTFUNCTION (11 COSL 8 COSE)]



WORKSPACE (COUNT)
WORKSPACE (SINRATE)
WORKSPACE (COSRATE)
WORKSPACE (SINADDER)
WORKSPACE (COSADDER)

START
OPENOUTPUT

PUT (+0) INTO (COUNT)
PUT (+0) INTO (SINRATE)
PUT (+127) INTO (COSRATE)
PUT (+0) INTO (SINADDER)
PUT (+0) INTO (COSADDER)

LOOP

IF (COUNT) MULTIPLEOF2TO (4)
BEGIN ONNEWLINE
      PRINT (COUNT)
      PRINTSIN (COUNT)
      PRINTCOS (COUNT)
      PRINT (SINRATE)
      PRINT (COSRATE)
END

ADD1TO (COUNT)
ADD (SINRATE) TO (SINADDER)
ADD (COSRATE) TO (COSADDER)

IF (SINADDER) OVER (+127)
BEGIN ADD (-1) TO (COSRATE)
      ADD (-128) TO (SINADDER)
END
IF (SINADDER) UNDER (+0)
BEGIN ADD1TO (COSRATE)
      ADD (+128) TO (SINADDER)
END


IF (COSADDER) OVER (+127)
BEGIN ADD1TO (SINRATE)
      ADD (-128) TO (COSADDER)
END
IF (COSADDER) UNDER (+0)
BEGIN ADD (-1) TO (SINRATE)
      ADD (+128) TO (COSADDER)

END

IF (COUNT) UNDER (+1024)
BEGIN GOTO (LOOP)
END

CLOSEOUTPUT
STOP
<! Halt !>